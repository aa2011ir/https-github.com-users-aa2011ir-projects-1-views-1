#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

piece  flare1, flare2, missileflare1, missileflare2, base, missilebox1, missilebox2, turretgun1,
       turretgun2, gunbarrel1, gunbarrel2, ground, turretmis1, turretmis2;

static-var  gun_3, restore_delay, gun_2, oldHead;

// Signal definitions
#define SIG_MOVE			2
#define SIG_AIM_2			4
#define SIG_AIM_3			8


#define BASEPIECE base
#define HITSPEED <20.0>
//how 'heavy' the unit is, on a scale of 1-10
#define UNITSIZE 3
#define MAXTILT 100
HitByWeapon(anglex, anglez, damage)	// angle[x|z] is always [-500;500], damage is multiplied by 100
{
	var amount;//, speedz, speedx;
	amount = damage / (100 * UNITSIZE);
	if (amount < 3  ) return (0);
	if (amount > MAXTILT) amount = MAXTILT;
	//get PRINT(anglex, anglez, amount, damage);
	//speedz = HITSPEED * get ABS(anglez) / 500; //nevermind this, the random error this produces actually looks better than the accurate version
	turn BASEPIECE to z-axis (anglez * amount) / 100  speed HITSPEED;
	turn BASEPIECE to x-axis <0> - (anglex * amount) /100 speed HITSPEED;
	wait-for-turn BASEPIECE around z-axis;
	wait-for-turn BASEPIECE around x-axis;
	turn BASEPIECE to z-axis <0.000000> speed HITSPEED / 4;
	turn BASEPIECE to x-axis <0.000000> speed HITSPEED / 4;
}
HitByWeaponId(anglex, anglez, weaponid, dmg) //weaponID is always 0,lasers and flamers give angles of 0
{
	start-script HitByWeapon(dmg, anglez,anglex); //I dont know why param order must be switched, and this also runs a frame later :(
	return (100); //return damage percent
}



SetDirection(heading)
{
	var spd;
	spd = (get ABS(oldHead) - get ABS(heading)) / 2;
	turn ground to y-axis heading speed spd;
	turn base to y-axis 0-heading speed spd;
	oldHead = heading;
}

RockOnWaves()
{
	var rockDir, rockAng, rockSpeed;
	rockDir = <-0.1>;
	rockAng = <-0.1>;
	while( TRUE )
	{
		while( get ABS(rockAng) < <1.31> )
		{
			rockSpeed = <1.45> - get ABS(rockAng);
			turn ground to x-axis rockAng speed rockSpeed*4;
			wait-for-turn ground around x-axis;
			rockAng = rockAng + rockDir;
		}
		rockDir = 0 - rockDir;
		rockAng = rockAng + rockDir + rockDir;
	}
}

SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 500;
	}
	start-script RockOnWaves();
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 258;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 257;
			}
			emit-sfx smoketype from base;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
}

Create()
{
	hide flare2;
	hide flare1;
	gun_3 = flare1;
	gun_2 = flare1;
	restore_delay = 3000;
	start-script SmokeUnit();
	turn turretgun2 to x-axis <-25> speed <25.000000>;
    spin turretgun1 around y-axis speed <25.0>;
	turn missilebox1 to x-axis <-25.000000> speed <25.000000>;
	turn missilebox2 to x-axis <-25.000000> speed <25.000000>;
}

SetMaxReloadTime(Func_Var_1)
{
	restore_delay = Func_Var_1 * 3;
}

static-var  Stunned;
ExecuteRestoreAfterDelay()
{
    if (Stunned) {
        return (1);
    }
	set-signal-mask 0;
	turn turretgun2 to x-axis <-25.000000> speed <25.000000>;
    spin turretgun1 around y-axis speed <25.0>;
	turn missilebox1 to y-axis <0.000000> speed <25.000000>;
	turn missilebox1 to x-axis <-25.000000> speed <25.000000>;
	turn missilebox2 to y-axis <0.000000> speed <25.000000>;
	turn missilebox2 to x-axis <-25.000000> speed <25.000000>;
}
SetStunned(State)
{
    Stunned = State;
	if (!Stunned) {
	    start-script ExecuteRestoreAfterDelay();
	}
}
RestoreAfterDelay()
{
	sleep restore_delay;
	start-script ExecuteRestoreAfterDelay();
}

waveSplash()
{
	while( TRUE )
	{
		turn base to x-axis <-3.0> speed <3.0>;
		move base to y-axis [0.23] speed [0.23];
		wait-for-turn base around x-axis;
		turn base to x-axis <0.0> speed <3.0>;
		move base to y-axis [0.0] speed [0.23];
		wait-for-turn base around x-axis;
	}
}

StartMoving()
{
	signal SIG_MOVE;
	set-signal-mask SIG_MOVE;
	var waketime;
	start-script waveSplash();
	while( TRUE )
	{
		emit-sfx 1024 + 1 from ground;
		waketime = (get CURRENT_SPEED) * 100 / get MAX_SPEED;
		if( waketime < 50 ) waketime = 50;
		sleep 14000 / waketime;
	}
}

StopMoving()
{
	signal SIG_MOVE;
	turn base to x-axis <0.0> speed <3.0>;
	move base to y-axis [0.0] speed [0.23];
}

AimSecondary(heading, pitch)
{
	signal SIG_AIM_2;
	set-signal-mask SIG_AIM_2;
	if( gun_2 )
	{
		turn missilebox2 to y-axis heading speed <455.000000>;
		turn missilebox2 to x-axis <0.000000> - pitch speed <430.000000>;
		wait-for-turn missilebox2 around y-axis;
		wait-for-turn missilebox2 around x-axis;
	}
	else
	{
		turn missilebox1 to y-axis heading speed <455.000000>;
		turn missilebox1 to x-axis <0.000000> - pitch speed <430.000000>;
		wait-for-turn missilebox1 around y-axis;
		wait-for-turn missilebox1 around x-axis;
	}
	start-script RestoreAfterDelay();
	return (1);
}

AimTertiary(heading, pitch)
{
	signal SIG_AIM_3;
	set-signal-mask SIG_AIM_3;
	turn turretgun1 to y-axis heading speed <755.000000>;
	turn turretgun2 to x-axis <0.000000> - pitch speed <730.000000>;
	wait-for-turn turretgun1 around y-axis;
	wait-for-turn turretgun2 around x-axis;
	start-script RestoreAfterDelay();
	return (1);
}

FireSecondary()
{
	gun_2 = !gun_2;
}

FireTertiary()
{
	if( gun_3 )
	{
	    emit-sfx 1024 + 0 from flare2;
		move gunbarrel2 to z-axis [-2.400000] speed [500.000000];
		sleep 100;
		wait-for-move gunbarrel2 along z-axis;
		move gunbarrel2 to z-axis [0.000000] speed [3.000000];
	}
	else
	{
	    emit-sfx 1024 + 0 from flare1;
		move gunbarrel1 to z-axis [-2.400000] speed [500.000000];
		sleep 100;
		wait-for-move gunbarrel1 along z-axis;
		move gunbarrel1 to z-axis [0.000000] speed [3.000000];
	}
	gun_3 = !gun_3;
}

AimFromPrimary(piecenum)
{
	piecenum = base;
}

AimFromSecondary(piecenum)
{
	piecenum = missilebox1 + gun_2;
}

AimFromTertiary(piecenum)
{
	piecenum = turretgun1;
}

QueryPrimary(piecenum)
{
	piecenum = base;
}

QuerySecondary(piecenum)
{
	piecenum = missileflare1 + gun_2;
}

QueryTertiary(piecenum)
{
	piecenum = gun_3;
}

/*
AimWeapon4(heading, pitch)
{
	sleep 100;
	return (1);
}

FireWeapon4()
{
	sleep 100;
}

QueryWeapon4(piecenum)
{
	sleep 100;
}

AimFromWeapon4(piecenum)
{
	piecenum = base;
}
*/

SweetSpot(piecenum)
{
	piecenum = base;
}

Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1 ;
		explode ground type BITMAPONLY | NOHEATCLOUD;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode turretgun1 type BITMAPONLY | NOHEATCLOUD;
		explode turretgun2 type BITMAPONLY | NOHEATCLOUD;
		explode gunbarrel1 type FIRE | SMOKE | FALL | NOHEATCLOUD;
		//explode flare1 type BITMAPONLY | NOHEATCLOUD;
		//explode gunbarrel2 type BITMAPONLY | NOHEATCLOUD;
		//explode flare2 type BITMAPONLY | NOHEATCLOUD;
		//explode turretmis1 type BITMAPONLY | NOHEATCLOUD;
		//explode missilebox1 type BITMAPONLY | NOHEATCLOUD;
		//explode missileflare1 type BITMAPONLY | NOHEATCLOUD;
		//explode turretmis2 type BITMAPONLY | NOHEATCLOUD;
		//explode missilebox2 type BITMAPONLY | NOHEATCLOUD;
		//explode missileflare2 type BITMAPONLY | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 50 )
	{
		corpsetype = 2 ;
		explode ground type FALL | NOHEATCLOUD;
		explode turretgun1 type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode turretgun2 type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode gunbarrel1 type FALL | NOHEATCLOUD;
		//explode flare1 type FALL | NOHEATCLOUD;
		//explode gunbarrel2 type FIRE | SMOKE | FALL | NOHEATCLOUD;
		//explode flare2 type FIRE | SMOKE | FALL | NOHEATCLOUD;
		//explode turretmis1 type FIRE | SMOKE | FALL | NOHEATCLOUD;
		//explode missilebox1 type FIRE | SMOKE | FALL | NOHEATCLOUD;
		//explode missileflare1 type FALL | NOHEATCLOUD;
		//explode turretmis2 type FIRE | SMOKE | FALL | NOHEATCLOUD;
		//explode missilebox2 type FALL | NOHEATCLOUD;
		//explode missileflare2 type FIRE | SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 99 )
	{
		corpsetype = 3 ;
		explode ground type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode turretgun1 type EXPLODE_ON_HIT | SMOKE | FALL | NOHEATCLOUD;
		explode turretgun2 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode gunbarrel1 type SMOKE | FALL | NOHEATCLOUD;
		//explode flare1 type SMOKE | FALL | NOHEATCLOUD;
		//explode gunbarrel2 type EXPLODE_ON_HIT | SMOKE | FALL | NOHEATCLOUD;
		//explode flare2 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		//explode turretmis1 type EXPLODE_ON_HIT | SMOKE | FALL | NOHEATCLOUD;
		//explode missilebox1 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		//explode missileflare1 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		//explode turretmis2 type FIRE | SMOKE | FALL | NOHEATCLOUD;
		//explode missilebox2 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		//explode missileflare2 type FIRE | SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	corpsetype = 3 ;
		explode ground type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode base type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode turretgun1 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode turretgun2 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode gunbarrel1 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		//explode flare1 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		//explode gunbarrel2 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		//explode flare2 type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		//explode turretmis1 type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		//explode missilebox1 type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		//explode missileflare1 type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		//explode turretmis2 type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		//explode missilebox2 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		//explode missileflare2 type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
	return corpsetype;
}
